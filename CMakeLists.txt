cmake_minimum_required(VERSION 3.24)
include(ExternalProject)

set(ProjectName psOff_third_party)
project(${ProjectName} VERSION 0.0.1)

set(INSTALL_DIR ${CMAKE_BINARY_DIR}/install)
set(BUILDS_DIR ${CMAKE_BINARY_DIR}/builds)

unset(CMAKE_IMPORT_LIBRARY_SUFFIX)
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

set(CMAKE_SHARED_LINKER_FLAGS "${CMAKE_SHARED_LINKER_FLAGS} /OPT:REF,ICF")
set(CMAKE_INSTALL_MESSAGE LAZY)

set(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS ON)
set(CMAKE_WINDOWS_SYMBOL_VISIBILITY_IN_STATIC_LIBRARIES OFF)

ExternalProject_Add(zlib_project
  SOURCE_DIR ${CMAKE_SOURCE_DIR}/projects/zlib
  BINARY_DIR ${BUILDS_DIR}/zlib
  CMAKE_ARGS
  -DCMAKE_BUILD_TYPE:STRING=Release
  -DCMAKE_INSTALL_PREFIX=${INSTALL_DIR}
  -DCMAKE_INSTALL_MESSAGE=${CMAKE_INSTALL_MESSAGE}
  -DCMAKE_C_FLAGS_RELEASE=${CMAKE_C_FLAGS_RELEASE}
  -DCMAKE_CXX_FLAGS_RELEASE=${CMAKE_CXX_FLAGS_RELEASE}
  -DCMAKE_SHARED_LINKER_FLAGS=${CMAKE_SHARED_LINKER_FLAGS}
)

install(FILES "projects/fff/fff.h" DESTINATION ${INSTALL_DIR}/include/fff)

# P7
add_subdirectory(projects/p7)
set_target_properties(p7
  PROPERTIES
  ARCHIVE_OUTPUT_DIRECTORY "${INSTALL_DIR}/lib"
  LIBRARY_OUTPUT_DIRECTORY "${INSTALL_DIR}/bin"
  RUNTIME_OUTPUT_DIRECTORY "${INSTALL_DIR}/bin"
)

install(DIRECTORY "projects/p7/Headers" DESTINATION ${INSTALL_DIR}/include/p7
  FILES_MATCHING PATTERN "*.h"
)

# SDL2
add_subdirectory(projects/SDL)
set_target_properties(SDL2
  PROPERTIES
  ARCHIVE_OUTPUT_DIRECTORY "${INSTALL_DIR}/bin"
  LIBRARY_OUTPUT_DIRECTORY "${INSTALL_DIR}/lib"
  RUNTIME_OUTPUT_DIRECTORY "${INSTALL_DIR}/bin"
)
install(DIRECTORY "${CMAKE_BINARY_DIR}/projects/SDL/include" DESTINATION ${INSTALL_DIR}
  FILES_MATCHING PATTERN "*.h"
)